\section{Appendix A: Answers to inline questions}
This sections contains solutions to the inline questions stated in the assignment description.
\subsection{Question 1}
\begin{equation*}
y[n] = z[Mn]
\end{equation*}
\begin{equation*}
z[n] = \sum_{0\leq j < 4L} h[j] \cdot q[n-j]
\end{equation*}
\begin{equation*}
q[n] = \begin{cases}
x[n \text{ div } L]&\text{if } n \bmod L = 0\\
0&\text{otherwise}
\end{cases}
\end{equation*}
\begin{equation*}
y[n] = \sum_{0\leq j<4L} h[j] \cdot q[nM-j]
\end{equation*}
\begin{equation*}
y[n] = \begin{cases}
\sum_{0 \leq j < 4L} h[j] \cdot q[(nM-j) \text{ div } L]&\text{if } (nM - j) \bmod L = 0\\
\sum_{0 \leq j < 4L} h[j] \cdot 0 &\text{otherwise}
\end{cases}
\end{equation*}
The \emph{otherwise} case is always 0 so doesn't contribute to the sum.
We can continue with just:
\begin{equation*}
y[n] = \sum_{0\leq j<4L} h[j] \cdot q[(nM-j) \text{ div } L])  \phantom{xxx}\text{ if } (nM-j) \bmod L = 0
\end{equation*}
$(nM-j) \bmod L = 0$  happens at:
\begin{itemize}
\item $  j = nM \bmod L$
\item $  j = nM \bmod L + L$
\item $  j = nM \bmod L + 2L$
\item $  j = nM \bmod L + 3L$
\end{itemize}
So four times in every summation. If we let $j$ run from $0$ to $4$ (excl),
we get $h[nM \bmod L + jL]$.
\begin{equation*}
y[n] = \sum_{0\leq j<4} h[nM \bmod L + jL] \cdot q[(nM-j \cdot L) \text{ div } L]
\end{equation*}
\begin{equation*}
y[n] = \sum_{0\leq j<4} h[nM \bmod L + jL] \cdot q[(nM) \text{ div } L - j]
\end{equation*}
Which is the equation from the assignment. 

\subsection{Question 2}
The period is $L$.

\subsection{Question 3} \label{sec:Q3}
We assume all values for $h[]$ are precomputed.
\begin{itemize}
\item Naive scaler directly
\begin{itemize}
\item Multiplications. The number of coefficients is $4L$. For each sample we need the multiply for each coefficient. The frequency of the FIR is $147$ times the output sample frequency. So the number of multiplications per output is $147 \cdot 640 = 94080$.
\item Coefficients. $4L = 4 \cdot 64 = 640$.
\item Highest sample rate. $94080 \cdot 48 \text{ kHz } \approx 4.516  \text{ GHz }$ assuming sequential FIR implementation.
\end{itemize}
\item Naive scaler composed
\begin{itemize}
\item Multiplications. We have $\frac{M_2}{L_2} \cdot M_1 \cdot (4 \cdot L_1) + M_2 \cdot (4 \cdot L_2) = \frac{14}{15} \cdot 63 \cdot (4 \cdot 64) + 14 \cdot (4 \cdot 15) = 15892.8$ multiplications per output. 
\item Coefficients. $4 \cdot L_1 + 4 \cdot L_2 = 4 \cdot 64 + 4\cdot 15 = 316$.
\item Highest sample rate. The second sub filter has the highest sample rate at $(4 \cdot 64) \cdot 63 \cdot \frac{14}{15} \cdot 48 \text{ kHz } \approx 722\text{ MHz }$, using sequential FIR.
\end{itemize}
\item Equation scaler directly
\begin{itemize}
\item Multiplications. In the direct formula the term $jL$ can be precomputed since $0\leq j<4$. The term $nM$ does not need a multiplier because $n$ is incremented by one each time, so we can just add $M$ to a stored value. For the same reason the \emph{mod} and \emph{div} do not need multipliers. In the end this only leave the multiplication for $h[]\cdot x[]$. This is done for each $j$ over the summation, so we need 4 multiplications. 
\item Coefficients. We need $4L = 640$ coefficients.
\item Highest sample rate. The filter runs at 4 times the output sample rate, $4 \cdot 48 \text{ kHZ } = 192$ kHz.
\end{itemize}
\item Equation scaler composed
\begin{itemize}
\item Multiplications. Using the same arguments as with the direct equation scaler we get $4 + 4 \cdot 14/15 \approx 7.7$ multiplications per output.
\item Coefficients. $4 \cdot 64 + 4\cdot 15 = 316$.
\item Highest sample rate. The second FIR runs at 4 times the output sample rate, $4 \cdot 48 \text{ kHZ } = 192$ kHz.
\end{itemize}
\end{itemize}
\section{Appendix B: Verilog source code}
This appendix includes Verilog source code for the \texttt{filter.v} file in the ISE project.

\begin{verbatim}
`timescale 1ns / 1ps

module filter
    #(parameter DWIDTH = 16,
        parameter DDWIDTH = 2*DWIDTH,
        parameter L = 160,
        parameter L_LOG = 8,
        parameter M = 147,
        parameter M_LOG = 8,
        parameter CWIDTH = 4*L)
    (input clk,
        input rst,
        output req_in,
        input ack_in,
        input [0:DWIDTH-1] data_in,
        output req_out,
        input ack_out,
        output [0:DWIDTH-1] data_out);

    // Output request register
    reg req_out_buf;
    assign req_out = req_out_buf;

    // Input request register
    reg req_in_buf;
    assign req_in = req_in_buf;

    // state counter. l = nM mod L (calculated efficiently using 
    // conditionals and addition/subtraction)
    reg [0:L_LOG-1] l;
    // The last 4 input samples used in the FIR
    reg signed [0:DWIDTH-1] in [0:3];

    // The FIR coefficients (lots of them)
    // Naively, we'd need [0:4L-1], but since the coefficients are
    // symmetric around h[2L], we can just reuse h[2L-1:1] for // h[2L+1:4L-1]
    reg signed [0:DWIDTH-1] h [0:2*L];
    
    // The 4 products in the sum are buffered as partial results for pipelining
    reg signed [0:DDWIDTH-1] partial1;
    reg signed [0:DDWIDTH-1] partial2;
    reg signed [0:DDWIDTH-1] partial3;
    reg signed [0:DDWIDTH-1] partial4;
    // Accumulator (lower bits assigned to output port directly)
    reg signed [0:DDWIDTH-1] sum;
    assign data_out = sum >> 15;

    initial
    begin
        // Initialize the ROM with coefficients from file
        $readmemh("coefficients.txt", h);
    end

    always @(posedge clk)
    begin
        // Reset => initialize
        if (rst)
        begin
            req_in_buf <= 0;
            req_out_buf <= 0;
            sum <= 0;
            l <= 0;
        end
        // !Reset => run
        else
        begin
            // Input
            if (req_in && ack_in)
            begin
                // Read new input sample, and shift older samples
                in[0] <= data_in;
                in[1] <= in[0];
                in[2] <= in[1];
                in[3] <= in[2];
                // Input is done
                req_in_buf <= 0;
            end

            // Output
            if (req_out && ack_out)
            begin
                // Output is done
                req_out_buf <= 0;
            end

            // No I/O, computation cycle
            if (!req_in && !req_out && !ack_in && !ack_out)
            begin
                // l <= (l + M) mod L, implemented with conditionals
                // No-overflow case
                if( l < L - M )
                begin
                    l <= l + M;
                end
                // Overflow case. Overflow also means we need a new input!
                else
                begin
                    l <= l - (L - M);
                    req_in_buf <= 1;
                end

                // We output after every computation cycle
                req_out_buf <= 1;

                // pipelined: sum <= (in[0] * h[l] + in[1] * h[l+L] + 
                //                  in[2] * h[l+L*2] + in[3] * h[l+L*3]);
                partial1 <= in[0] * h[l];      // h[l+L*0]
                partial2 <= in[1] * h[l+L];    // h[l+L*1]
                partial3 <= in[2] * h[L*2-l];  // h[l+L*2], mirrored
                partial4 <= in[3] * h[L-l];    // h[l+L*3], mirrored
                sum <= partial1 + partial2 + partial3 + partial4;
            end
        end
    end

endmodule

\end{verbatim}

